grammar org.eclipse.papyrus.transition.editor.xtext.UmlTransition
		with org.eclipse.xtext.common.Terminals
		
import "http://www.eclipse.org/uml2/3.0.0/UML" as uml

generate umlTransition "http://www.eclipse.org/papyrus/transition/editor/xtext/UmlTransition"

TransitionRule:
	(triggers+=EventRule (',' triggers+=EventRule)*)?
	(guard=GuardRule)? 
	(effect=EffectRule)? ;

//////////////////////
// EVENTS
//////////////////////

EventRule :
	CallOrSignalEventRule 
	| AnyReceiveEventRule
	| TimeEventRule 
	| ChangeEventRule ;

CallOrSignalEventRule :
	operationOrSignal = [uml::NamedElement] ;

AnyReceiveEventRule :
	isAReceiveEvent = 'all' ;
	
TimeEventRule :
	RelativeTimeEventRule | AbsoluteTimeEventRule ;

RelativeTimeEventRule :
	'after' expr=STRING;

AbsoluteTimeEventRule :
	'at' expr=STRING;

ChangeEventRule :
	'when' exp=STRING;

///////////////////////
// GUARD
///////////////////////

GuardRule :
	'[' constraint=STRING']';
	
///////////////////////
// EFFECT
///////////////////////

EffectRule :
	'/' kind = BehaviorKind
	behaviorName = ID ;

enum BehaviorKind :
	ACTIVITY = "Activity" |
	STATE_MACHINE = "StateMachine" |
	OPAQUE_BEHAVIOR = "OpaqueBehavior" ;